     H nomain option(*NOSHOWCPY)
      /copy cpy,u6int_h
      /copy cpy,u6tree_h
      /copy cpy,u6xml_h
      /copy cpy,u6env_h

      /copy cpy,u6fmt_h
      //‚--------------------------------------------------------------------
      //‚get format from list
      //‚--------------------------------------------------------------------
     pfmt_getFormat    b                   export
     d fmt_getFormat   pi              *
     d  lFmts                          *
     d  fmtID                        10a   const varying
     d  fAppli                        1a   const options(*nopass)
      *
     d  lFmt           s               *
       if lFmts=*null;
         lFmts=tree_getnewlink(
                  tree_getNewItem(%addr(tFormats):%size(tFormats)));
       endif;
       lFmt=tree_getLinkFromList(lFmts:kformat:fmtID);
       if lFmt=*null;
         if %parms()=%parmNum(fAppli) and fAppli='Y';
           lFmt=tree_Xml2Tree(env_getAppliPath()+fmtID+'.fmt'
                                :%pAddr(fmt_xmlInput));
         else;
           lFmt=tree_Xml2Tree(env_getClientPath()+fmtID+'.fmt'
                                :%pAddr(fmt_xmlInput));
         endIf;
         if lFmt<>*null;
           fmt_SubFormatsFormulaLoad(lFmt);
           tree_linktoparent(lFmts:lFmt);
         endif;
       endIf;
       return lFmt;
     p                 e
      //‚-------------------------------------------------------------------
      //‚get item for FORMAT family
      //‚-------------------------------------------------------------------
     pfmt_XmlInput     b                   export
     d fmt_xmlInput    pi              *
     d   ND                                const likeDs(xml_nodeDefine)
      *
     d   fmt           ds                  likeds(tformat)    based(pFmt)
     d   fields        ds                  likeds(tFields)    based(pFields)
     d   field         ds                  likeds(tField)     based(pField)
     d   subFmts       ds                  likeds(tSubFormats)based(pSubFmts)
     d   subFmt        ds                  likeds(tSubFormat) based(pSubFmt)
       if ND.ID='FORMAT';
         pFmt=tree_getnewitem(%addr(tFormat):%size(tFormat));
         fmt.ID    =xml_getAttAsString('ID':ND.atts);
         fmt.len   =xml_getAttAsInteger('LEN':ND.atts);
         fmt.fldnbr=xml_getAttAsInteger('FLDNBR':ND.atts);
         fmt.pBuffer1=*null;
         fmt.pBuffer0=*null;
         return pFmt;

       elseIf ND.ID='FIELDS';
         pFields=tree_getnewitem(%addr(tFields):%size(tFields));
         return pFields;

       elseif ND.ID='FIELD';
         pField=tree_getnewitem(%addr(tField):%size(tField));
         field.ID  =xml_getAttAsString('ID':ND.atts);
         field.NO  =xml_getAttAsInteger('NO':ND.atts);
         field.type=xml_getAttAsString('TYPE':ND.atts);
         field.Len =xml_getAttAsInteger('LEN':ND.atts);
         field.dec =xml_getAttAsInteger('DEC':ND.atts);
         field.digits=xml_getAttAsInteger('DIGITS':ND.atts);
         field.posinbuffer=xml_getAttAsInteger('POS':ND.atts)-1;
         return pField;

       elseIf ND.ID='SUBFORMATS';
         pSubFmts=tree_getnewitem(%addr(tSubFormats):%size(tSubFormats));
         return pSubFmts;

       elseIf ND.ID='SUBFORMAT';
         pSubFmt=tree_getnewitem(%addr(tSubFormat):%size(tSubFormat));
         SubFmt.pos=xml_getAttAsInteger('POS':ND.atts)-1;
         SubFmt.formula=xml_getAttAsString('FORMULA':ND.atts);
         return pSubFmt;

       endIf;
       return *null;
     p                 e
      //‚------------------------------------------------------------------*
      //‚set item for FORMAT family
      //‚-------------------------------------------------------------------
     pfmt_XMLoutput    b                   export
     d fmt_XMLoutput   pi                  likeDs(xml_nodeDefine)
     d   lNode                         *   const
      *
     d   ND            ds                  likeDs(xml_nodeDefine)
     d   fmt           ds                  likeds(tFormat)    based(pFmt)
     d   fields        ds                  likeds(tFields)    based(pFields)
     d   field         ds                  likeds(tField)     based(pField)
     d   subFmts       ds                  likeds(tSubformats)based(pSubFmts)
     d   subFmt        ds                  likeds(tSubformat) based(pSubFmt)
       clear ND;

       if tree_isofthekind(kFormat:lNode:pFmt);
         ND.ID='FORMAT';
         xml_setAttfromString (ND.atts:'ID':fmt.ID);
         xml_setAttfromInteger(ND.atts:'LEN':fmt.len);
         xml_setAttfromInteger(ND.atts:'FLDNBR':fmt.fldnbr);

       elseIf tree_isofthekind(kFields:lNode:pFields);
         ND.ID='FIELDS';

       elseif tree_isofthekind(kField:lNode:pField);
         ND.ID='FIELD';
         xml_setAttfromString (ND.atts:'ID':field.ID);
         xml_setAttfromInteger(ND.atts:'NO':field.NO);
         xml_setAttfromString (ND.atts:'TYPE':field.type);
         xml_setAttfromInteger(ND.atts:'LEN':field.len);
         if %scan(field.type:'PS')>0;
           xml_setAttfromInteger(ND.atts:'DIGITS':field.digits);
           xml_setAttfromInteger(ND.atts:'DEC':field.dec);
         endIf;
         xml_setAttfromInteger(ND.atts:'POS':field.posinbuffer);

       elseIf tree_isofthekind(kSubFormats:lNode:pSubFmts);
         ND.ID='SUBFORMATS';

       elseIf tree_isofthekind(kSubFormat:lNode:pSubFmt);
         ND.ID='SUBFORMAT';
         xml_setAttfromInteger(ND.atts:'POS':SubFmt.pos);
         xml_setAttfromString (ND.atts:'FORMULA':SubFmt.formula);

       endIf;

       return ND;
     p                 e
      //‚------------------------------------------------------------------*
      //‚for SubFormats load there formula
      //‚-------------------------------------------------------------------
     pfmt_SubFormatsFormulaLoad...
     p                 b
     d fmt_SubFormatsFormulaLoad...
     d                 pi
     d  lFmt                           *
      *
     d  lSubFmts       s               *
     d  lSubFmt        s               *
     d  SubFmt         ds                  likeDs(tSubFormat)based(pSubFmt)
      *
       lSubFmts=tree_getLinkFromList(lFmt:kSubformats);
       if lSubFmts=*null;
         return;
       endif;
       lSubFmt=tree_getFirst(lSubFmts);
       dow lSubFmt<>*null;
         pSubFmt=tree_getItem(lSubFmt);
         subFmt.lFormula=int_FormulaLoad(subFmt.Formula:lFmt);
         lSubFmt=tree_getNext(lSubFmt);
       endDo;
     p                 e
